allprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    group 'org.deepboof'
    version '0.4-SNAPSHOT'
}


subprojects {
    apply plugin: 'java'
    apply plugin: 'osgi'
    apply plugin: 'maven'
    apply plugin: 'signing'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    repositories {
        mavenCentral()
        mavenLocal()

        maven {
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }

    dependencies {
        compile group: 'org.ddogleg', name: 'ddogleg', version: '0.11-SNAPSHOT'

        testCompile group: 'junit', name: 'junit', version: '4.12'
    }

    sourceSets {
        test {
            java {
                srcDir 'autogenerate/java'
            }
        }
    }

    test {
        ignoreFailures true
        reports.html.enabled = false
    }

    javadoc {
        configure(options) {
            failOnError = false
        }
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    // if Maven central isn't setup in gradle.properties skip all of this
    if( project.hasProperty('ossrhUsername') ) {
        signing {
            sign configurations.archives
        }

        uploadArchives {
            repositories {
                mavenDeployer {
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }

                    snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }

                    pom.project {
                        name 'DeepBoof'
                        packaging 'pom'
                        // optionally artifactId can be defined here
                        description 'Trainer Agnostic Deep Learning'
                        url 'http://boofcv.org'

                        scm {
                            connection 'scm:git:git://github.com/lessthanoptimal/DeepBoof.git'
                            developerConnection 'scm:git:git://github.com/lessthanoptimal/DeepBoof.git'
                            url 'https://github.com/lessthanoptimal/BoofCV'
                        }

                        licenses {
                            license {
                                name 'The Apache Software License, Version 2.0'
                                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            }
                        }

                        developers {
                            developer {
                                id 'pabeles'
                                name 'Peter Abeles'
                                email 'peter.abeles@gmail.com'
                            }
                        }
                    }
                }
            }
        }
    }
}

idea {
    project {
        jdkName = '1.7 (64bit)'
        languageLevel = '1.7'
    }

    module {
        excludeDirs += file('.idea')
    }
}

// Disable the creation of jars for distribution.  If you don't do this it will crash
[':modules'].each {String a ->
    project(a) {
        if( project.hasProperty('ossrhUsername') ) {
            signArchives.enabled = false
        }
        sourcesJar.enabled = false
        javadocJar.enabled = false
        jar.enabled = false
        uploadArchives.enabled = false
        install.enabled = false
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}