import org.gradle.api.tasks.testing.logging.TestExceptionFormat

plugins {
    id "com.peterabeles.gversion" version "1.10.2" apply false
}

allprojects {
    apply plugin: 'eclipse'
    apply plugin: 'com.peterabeles.gversion'

    group 'org.deepboof'
    version '0.5.2'

    createVersionFile.enabled = false // run only once. enabled in types
}

project.ext.lombok_version = '1.18.24'
project.ext.jabel_version = '1.0.0'
project.ext.jetnull_version = '23.0.0'
project.ext.jsr250_version = '1.0'
project.ext.junit_version = '5.9.1'
project.ext.auto64to32_version = '3.2.1'
project.ext.zip4j_version = '2.11.5'
project.ext.protobuf_version = '3.23.2'
project.ext.jarchivelib_version = '1.2.0'
project.ext.jcommander_version = '1.82'
project.ext.jgraphx_version = '4.2.2'

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    java {
        withJavadocJar()
        withSourcesJar()
        toolchain { languageVersion = JavaLanguageVersion.of(17) }
    }

    // Prevents tons of errors if someone is using ASCII
    tasks.withType(JavaCompile).configureEach { options.encoding = "UTF-8" }

    // Creates Java 11 byte code
    tasks.withType(JavaCompile).configureEach { options.release = 11 }

    // Enable incremental compile. Should make single file changes faster
    tasks.withType(JavaCompile) { options.incremental = true }

    // Fail on jar conflict
    configurations.all { resolutionStrategy { failOnVersionConflict() } }

    repositories {
        mavenCentral()
        mavenLocal()

        maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url = 'https://jitpack.io' } // Allows annotations past Java 8 to be used
    }

    dependencies {
        implementation group: 'org.ddogleg', name: 'ddogleg', version: '0.23'

        compileOnly "org.jetbrains:annotations:$project.jetnull_version" // @Nullable
        compileOnly "javax.annotation:jsr250-api:$project.jsr250_version"  // @Generated
        compileOnly "org.projectlombok:lombok:$project.lombok_version"
        testImplementation project.sourceSets.main.compileClasspath

        testImplementation("org.junit.jupiter:junit-jupiter-api:$junit_version")
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:$junit_version")

        // needed to use Java 11+ syntax with Java 1.8 byte code
        annotationProcessor("com.github.bsideup.jabel:jabel-javac-plugin:${project.jabel_version}")
        testAnnotationProcessor("com.github.bsideup.jabel:jabel-javac-plugin:${project.jabel_version}")
    }

    sourceSets {
        autogenerate { java { srcDir 'autogenerate/java' } }
    }

    test {
        useJUnitPlatform()
        reports.html.enabled = false
        // Make the error logging verbose to make debugging on CI easier
        testLogging.showStandardStreams = true
        testLogging.exceptionFormat TestExceptionFormat.FULL
        testLogging.showCauses true
        testLogging.showExceptions true
        testLogging.showStackTraces true
    }

    javadoc {
        configure(options) {
            links = ['http://docs.oracle.com/javase/8/docs/api/',
                     'http://ejml.org/javadoc/',
                     'http://ddogleg.org/javadoc/']
            failOnError = false
            enabled = !project.version.contains("SNAPSHOT") // disable to stop it from spamming stdout
        }

        // https://github.com/gradle/gradle/issues/11182 Error introduced in JDK 11
        if (JavaVersion.current().compareTo(JavaVersion.VERSION_1_9) >= 0) {
            options.addStringOption("-release", "8")
        }

        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }

    // Disable the creation of jars for distribution.  If you don't do this it will crash
    if (['modules','examples'].contains(name)) {
        project.jar.enabled = false
        project.tasks.publish.enabled = false
    }

    if (!project.tasks.publish.enabled)
        return

    // if Maven central isn't setup in ~/.gradle/gradle.properties fill in these variables to make it happy
    if( !project.hasProperty('ossrhUsername') ) {
        ext.ossrhUsername = "dummy"
        ext.ossrhPassword = "dummy"
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                pom {
                    name = 'DeepBoof'
                    description = 'Trainer Agnostic Deep Learning'
                    url = 'https://boofcv.org'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'pabeles'
                            name = 'Peter Abeles'
                            email = 'peter.abeles@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/lessthanoptimal/DeepBoof.git'
                        developerConnection = 'scm:git:git://github.com/lessthanoptimal/DeepBoof.git'
                        url = 'https://github.com/lessthanoptimal/DeepBoof'
                    }
                }
            }
        }
        repositories {
            maven {
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username ossrhUsername
                    password ossrhPassword
                }
            }
        }
    }

    if (ext.ossrhPassword != "dummy") {
        signing { sign publishing.publications.mavenJava }
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.BIN
    gradleVersion = '7.3.3'
}
